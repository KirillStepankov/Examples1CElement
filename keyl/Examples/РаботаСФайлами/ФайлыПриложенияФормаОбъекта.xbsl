
@Обработчик
метод ПослеЧтения()
    если Объект.Файл != Неопределено
        Компоненты.СписокФайлов.Значение = [Объект.Файл]
    иначе если Компоненты.СписокФайлов.Значение.Размер() != 0
        Компоненты.СписокФайлов.Значение.Очистить()
    ;
;

метод СписокФайловПриЗагрузкеФайла(Источник: СписокФайлов, Событие: СобытиеСДанными<ДвоичныйОбъект.Ссылка>)
    Объект.Файл = Событие.Данные
    если Объект.Наименование.Пусто()
        Объект.Наименование = ИмяФайла(Файл = Объект.Файл)
    ;
;

метод СписокФайловПриУдаленииФайла(Источник: СписокФайлов, Событие: СобытиеСДанными<ДвоичныйОбъект.Ссылка>)
    Объект.Файл = Неопределено
;

@НаСервере @ДоступноСКлиента
статический метод ИмяФайла(Файл: ДвоичныйОбъект.Ссылка?): Строка
    возврат Файл.Загрузить().ИмяФайла
;

@НаСервере @ДоступноСКлиента
статический метод HttpСсылкаФайла(Файл: ДвоичныйОбъект.Ссылка): ДвоичныйОбъект.HttpСсылка
    возврат Файл.СоздатьHttpСсылку(ТаймаутУстаревания = 5м, ТребуетсяАутентификация = Ложь)
;

метод HttpСсылкаНаСкачиваниеФайлаОбработчик(Команда: ОбычнаяКоманда)
    знч HttpСсылкаФайла = HttpСсылкаФайла(Объект.Файл).Абсолютная()
    БуферОбмена.Записать(HttpСсылкаФайла)
    новый Уведомление(
        Заголовок = "Скопировано",
        Текст = "Ссылка скопирована в буфер обмена.",
        Вид = ВидУведомления.Всплывающее
    ).Показать()
;
